#
# (C) Copyright 2005- ECMWF.
#
# This software is licensed under the terms of the Apache Licence Version 2.0
# which can be obtained at http://www.apache.org/licenses/LICENSE-2.0.
#
# In applying this licence, ECMWF does not waive the privileges and immunities
# granted to it by virtue of its status as an intergovernmental organisation
# nor does it submit to any jurisdiction.
#

list( APPEND ecc_tools_sources
             grib_tools.c
             grib_options.c
             grib_tools.h )

if( EC_OS_NAME MATCHES "windows" )
  list( APPEND ecc_tools_sources wingetopt.c )
endif()

if( HAVE_EXPERIMENTAL_BUILD_WITH_CXX )
    foreach( _src ${ecc_tools_sources} )
        get_filename_component( _ext ${_src} EXT )
        if( _ext STREQUAL ".c" )
            set_source_files_properties(${_src} PROPERTIES LANGUAGE CXX)
        endif()
    endforeach()
endif()

# tools library
ecbuild_add_library( TARGET    ecc_tools
                     TYPE      STATIC
                     NOINSTALL
                     SOURCES   ${ecc_tools_sources}
                     PRIVATE_LIBS      eccodes )

# tools binaries
list( APPEND ecc_tools_binaries
             codes_info codes_count codes_split_file
             grib_histogram grib_filter grib_ls grib_dump grib_merge
             grib2ppm grib_set grib_get grib_get_data grib_copy
             grib_compare codes_parser grib_index_build bufr_index_build
             bufr_ls bufr_dump bufr_set bufr_get
             bufr_copy bufr_compare
             gts_get gts_compare gts_copy gts_dump gts_filter gts_ls
             metar_dump metar_ls metar_compare metar_get metar_filter metar_copy )

list( APPEND ecc_tools_binaries_extra
             big2gribex
             gg_sub_area_check
             grib_repair
             grib_to_json
             codes_export_resource
             grib_check_gaussian_grid
             bufr_split_by_rdbSubtype )

# Install generic tools
foreach( tool ${ecc_tools_binaries} )
    # here we use the fact that each tool has only one C file that matches its name
    if( HAVE_EXPERIMENTAL_BUILD_WITH_CXX )
        set_source_files_properties(${tool}.c PROPERTIES LANGUAGE CXX)
    endif()
    ecbuild_add_executable( TARGET     ${tool}
                            SOURCES    ${tool}.c
                            LIBS       ecc_tools )
endforeach()

# Install extra tools
# User must run cmake with -DECCODES_INSTALL_EXTRA_TOOLS=ON
foreach( tool ${ecc_tools_binaries_extra} )
    ecbuild_add_executable( TARGET     ${tool}
                            SOURCES    ${tool}.c
                            CONDITION  ECCODES_INSTALL_EXTRA_TOOLS
                            LIBS       ecc_tools )
endforeach()

# grib_count/bufr_count etc. Same source code, different executable names
ecbuild_add_executable( TARGET     grib_count
                        SOURCES    codes_count.c
                        LIBS       ecc_tools )
ecbuild_add_executable( TARGET     bufr_count
                        SOURCES    codes_count.c
                        LIBS       ecc_tools )
ecbuild_add_executable( TARGET     gts_count
                        SOURCES    codes_count.c
                        LIBS       ecc_tools )

# grib to netcdf is optional
ecbuild_add_executable( TARGET     grib_to_netcdf
                        SOURCES    grib_to_netcdf.c
                        CONDITION  HAVE_NETCDF
                        LIBS       ecc_tools NetCDF::NetCDF_C )


ecbuild_add_executable( TARGET     grib_list_keys
                        SOURCES    list_keys.c
                        CONDITION  ECCODES_INSTALL_EXTRA_TOOLS
                        LIBS       ecc_tools )

ecbuild_add_executable( TARGET     codes_bufr_filter
                        SOURCES    bufr_filter.c
                        LIBS       ecc_tools )


# grib1to2 script needs to be generated before installation
if( ECCODES_INSTALL_EXTRA_TOOLS )
    ecbuild_info("ECCODES_INSTALL_EXTRA_TOOLS enabled")
    configure_file( grib1to2.in  grib1to2 )
    install( FILES ${CMAKE_CURRENT_BINARY_DIR}/grib1to2
             DESTINATION ${INSTALL_BIN_DIR}
             PERMISSIONS OWNER_EXECUTE OWNER_WRITE OWNER_READ
                         GROUP_EXECUTE GROUP_READ
                         WORLD_EXECUTE WORLD_READ )
endif()
########################################

# BUFR compare script
install( FILES ${CMAKE_CURRENT_SOURCE_DIR}/bufr_compare_dir
         DESTINATION ${INSTALL_BIN_DIR}
         PERMISSIONS OWNER_EXECUTE OWNER_WRITE OWNER_READ
                     GROUP_EXECUTE GROUP_READ
                     WORLD_EXECUTE WORLD_READ )


# bufr_filter script (See ECC-205)
execute_process( COMMAND ${CMAKE_COMMAND} -E copy_if_different
    ${PROJECT_SOURCE_DIR}/tools/bufr_filter
    ${CMAKE_BINARY_DIR}/bin/ )

install( FILES ${CMAKE_CURRENT_SOURCE_DIR}/bufr_filter
         DESTINATION ${INSTALL_BIN_DIR}
         PERMISSIONS OWNER_EXECUTE OWNER_WRITE OWNER_READ
                     GROUP_EXECUTE GROUP_READ
                     WORLD_EXECUTE WORLD_READ )

